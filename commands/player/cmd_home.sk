command /home [<text>]:

	prefix: kockacraft
	executable by: players
	usage: &f/home &a<otthon neve>
	description: Elteleportálás az otthonodhoz
	trigger:

		if arg-1 is not set:

			loop {home::%uuid of player%::*}:
				add loop-index to {_loop::*}
			set {_otthonok::*} to {_loop::*} ? "&c-"

			set {_otthonok} to "%{_otthonok::*}%"
			replace all " and" with "," in {_otthonok}

			set {_max_homes} to get_maximum_homes(player)
			set {_current_homes} to size of {home::%uuid of player%::*} ? 0

			set {_max} to "&7(%{_current_homes}%&7/%{_max_homes}%)"

			send server_feedback("casual", "&fOtthonjaid %{_max}%&f: ß%{_otthonok}%")

		else:
			if {home::%uuid of player%::%arg-1%} is not set:
				send server_feedback("error", "&cNincs otthonod &4%arg-1% &cnévvel!")
				stop

			teleport player to {home::%uuid of player%::%arg-1%}
			send server_feedback("casual", "&fTeleportálás a(z) ß%arg-1% &fotthonodhoz...")
			
on tab complete of "/home":
	set tab completions for position 1 to indices of {home::%uuid of player%::*}