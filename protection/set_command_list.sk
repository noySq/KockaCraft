on load:

	# AuthMe dolgokat manuálisan hozzáadjuk, mivel nem Skript parancsok
	add "register", "reg" to {-commandlist::*}
	add "login", "log", "l" to {-commandlist::*}
	
	# Loopoljuk az összes Skript parancsot, aminek nem kell permission, és hozzáadjuk egy listához
	# A lista újraindítás után kitörlődik
	loop all script commands:
		# Alapvetően ez csak a játékosoknak van kitalálva
		# Jogosultsággal rendelkező emberek kivételek
		# Ezért csak azokat a parancsokat kell felvenni, amiknek nincs jogosultságuk
		if permission of command loop-value is "":
			add loop-value to {-commandlist::*}

on send command list:
	
	# Fentebb említett jogosultság miatt cancel
	if check_permission(player, "bypass-command-list", "command-list") = true:
		stop
	
	# Játékosok számára látható parancs lista beállítása
	set command list to {-commandlist::*}

on command:

	# Konzolt külön kell kezelni, mivel nem egy játékos
	if executor is console:
		stop

	# Fentebb említett jogosultság miatt cancel
	if check_permission(executor, "bypass-command-list", "command-list") = true:
		stop

	# Ha nincs benne a listában a parancs amit futtatni szeretne, akkor cancel
	if {-commandlist::*} doesn't contain command:

		send server_feedback("warning", "&6Ismeretlen parancs! &7(/%command%&7)")
		cancel event