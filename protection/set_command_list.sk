on load:

	# AuthMe dolgokat manuálisan hozzáadjuk, mivel nem Skript parancsok
	add "register", "reg" to {-commandlist::*}
	add "login", "log", "l" to {-commandlist::*}
	
	# Loopoljuk az összes Skript parancsot, aminek nem kell permission, és hozzáadjuk egy listához
	# A lista újraindítás után kitörlődik
	loop all script commands:
		# Alapvetően ez csak a játékosoknak van kitalálva
		# Jogosultsággal rendelkező emberek kivételek
		# Ezért csak azokat a parancsokat kell felvenni, amiknek nincs jogosultságuk
		if permission of command loop-value is "":
			add loop-value to {-commandlist::*}

on send command list:
	
	# Fentebb említett jogosultság miatt cancel
	if player has permission "admin":
		stop

	# Játékosok számára látható parancs lista beállítása
	set command list to {-commandlist::*}

on command:

	# Konzolt külön kell kezelni, mivel nem egy játékos
	if executor is console:
		stop

	# Fentebb említett jogosultság miatt cancel
	if executor has permission "admin":
		stop

	# Ha nincs benne a listában a parancs amit futtatni szeretne, akkor cancel
	if {-commandlist::*} doesn't contain command:

		# Kókány szar módszer, de megteszi
		send server_feedback("warning", "%{config::global::server::message::unknown-command}% &7&o[/%command%]")
		cancel event