function ranks_add(p: player, target: offlineplayer, rank: text, time_for: timespan = 0 seconds):

	# Ha nem létezik a rang, cancel
	if {ranks::%{_rank}%::name} is not set:
		send server_feedback("error", {config::ranks::message::not-exist}) to {_p}
		stop

	# Ha a {_target} nem volt még fent, cancel
	if {_target} has not played before:
		send server_feedback("error", {config::ranks::message::never-played}) to {_p}
		stop

	# Odaadjuk a rangot a {_target}-nek
	set {rank::%uuid of {_target}%} to {_rank}

	# Permission kezelő
	#execute console command "/lp user %{_jatekos}% parent add %{_rang}%"

	# Frissítjük a tablistán a játékos nevét
	tablist_name({_target})

	# Hozzáadjuk {_target}-et a rang Team-jéhez
	ranks_team_add({_target})

	# Ha lett megadva lejárati dátum, beállítjuk
	if {_time_for} > 0 seconds:
		set {rank::%uuid of {_target}%::expire} to unix timestamp of {_time_for} from now
		set {_expire_date} to unix date of {rank::%uuid of {_target}%::expire}

	set {_prefix} to colored {ranks::%{_rank}%::prefix}

	server_log("rank", {config::ranks::message::rank-given-to})

	# Ha {_target} elérhető, értesítjük róla
	if {_target} is online:

		#setTablistName({_jatekos})

		send "" to {_target}
		send server_feedback("casual", {config::ranks::message::got-rank}) to {_target}

		if {_time_for} > 0 seconds:
			send server_feedback("warning", {config::ranks::message::expiration-date}) to {_target}

		send "" to {_target}