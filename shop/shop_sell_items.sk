options:
	tax: 0.05
	expiration: 7 days

	no-item-in-hand: "&cNincs semmilyen tárgy a kezedben!"
	asking-price-too-low: "&cAz eladás összege nem lehet kevesebb mint 1$!"
	cant-pay-tax: "&cNincs elég pénzed felrakni a tárgyad!"

function shop_sell_items(p: player, money: number):

	set {_item} to tool of {_p}
	# Ha a játékos kezében nincs semmi, stop
	if {_item} = air:
		send server_feedback("error", {@no-item-in-hand}) to {_p} 
		stop

	# Ha a kért összeg kevesebb mint 0, stop
	if {_money} < 0:
		send server_feedback("error", {@asking-price-too-low}) to {_p} 
		stop

	set {_asking_price_after_tax} to {_money} * {@tax}
	# Ha nincs pénz kifizetni az adót, stop
	if {egyenleg::%uuid of {_p}%} < {_asking_price_after_tax}:
		send server_feedback("error", {@cant-pay-tax}) to {_p} 
		stop

	# Megnöveljük egyel a Shop itemek globális ID-ját
	add 1 to {ShopID}

	# Lekérdezzük az NBT-jét
	set {_nbt} to custom nbt compound of {_item}
	# Beállitjuk a lejárati dátumot x nappal későbbre
	set {_expiration_date} to {@expiration} from now

	# Beállítjuk a bolttal kapcsolatos adatokat, később kelleni fog
	set int tag "Shop;ID" of {_nbt} to {ShopID}
	set float tag "Shop;Price" of {_nbt} to {_money}
	set string tag "Shop;Seller" of {_nbt} to name of {_p}
	set int tag "Shop;Expiration" of {_nbt} to unix timestamp of {_expiration_date}

	# Elmentjuk magát a tárgyat egy változóba, amit majd visszakérhetünk
	# Egyébként ez már nem az eredeti tárgy, hanem csak egy másolata
	set {shop::%uuid of {_p}%::%{ShopID}%} to {_item}

	# Kitöröljük az eredeti tárgyat
	delete {_p}'s tool
	# Rich get richer, poor stays poor (levonjuk az adót)
	remove {_asking_price_after_tax} from {egyenleg::%{_p}%}