options:
	shop-name: "%display_name({_owner})% &8boltja"
	gui-size: 6

	filler-item: black stained glass panes
	page-item: arrow

	next-page: "&aKövetkező oldal"
	previous-page: "&cElőző oldal"

function shop_gui(p: player, owner: offlineplayer, page: integer):

	# Létrehozunk egy GUI-t
	set {_gui} to chest inventory with {@gui-size} rows named {@shop-name}

	# Kiszámoljuk a maximum használható slotokat
	# Két sort kiveszünk belőlle
	set {_max_slot} to ( ( {@gui-size} - 2 ) * 9 )

	# Megnézzük, hogy mennyi tárgy van a játékos boltjában
	set {_items_num} to 0

	# Felülre és alulra egy kis díszítés
	set slot (integers between 0 and 8) of {_gui} to {@filler-item}
	set slot (integers between 45 and 53) of {_gui} to {@filler-item}

	# Végigmegyünk a játékos boltjának tárgyain
	loop get_paginated_objects({_page}, {_max_slot}, reversed {shop::%uuid of {_owner}%::*}):

		# Lekérdezzük az NBT-jét a tárgynak
		set {_nbt} to custom nbt compound of loop-value

		# Lekérdezzük a lejárati dátumot
		set {_unix_expiration} to int tag "Shop;Expiration" of {_nbt}
		# Mostani dátum Unix formátumban
		set {_unix_now} to unix timestamp of now

		# Ha a tárgy még nem járt le, folytatjuk
		if {_unix_now} < {_unix_expiration}:

			# Hozzáadjuk a tárgyat a GUI-ba
			add shop_get_items({_p}, loop-value) to {_gui}

			# Hozzáadunk egyet a váltózóhoz
			add 1 to {_items_num}

	# Ha ugyanannyi tárgy van mint hely, berakjuk az oldalváltó gombot
	if {_items_num} = {_max_slot}:
		set slot 50 of {_gui} to {@page-item} named {@next-page}

	# Ha nem az első oldalon vagyunk, berakjuk a visszafele gombot
	if {_page} > 1:
		set slot 48 of {_gui} to {@page-item} named {@previous-page}

	# Beállítjuk, hogy tudjuk kinek a boltját nézi a játékos
	set (metadata "current_shop_gui" of {_p}) to {_owner}
	set (metadata "current_shop_page" of {_p}) to {_page}

	# Megnyitjuk a GUI-t
	open {_gui} to {_p}